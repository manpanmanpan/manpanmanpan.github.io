---
layout:     post
title:      SAS
subtitle:   
date:       2019-03-23
author:     Cassie Pan
header-img: img/post-bg-rwd.jpg 
catalog: true
tags:
    - SAS
---
## SAS Notes

## 1. Reading External 
## 1.1 Reading Raw Data

``` SAS
* Create a SAS data set named contest;
* Read the file Pumpkin.dat using formatted input;
DATA contest;
   INFILE 'c:\MyRawData\Pumpkin.dat';
   INPUT Name $16. Age 3. +1 Type $1. +1 Date MMDDYY10.
         (Score1 Score2 Score3 Score4 Score5) (4.1);
RUN;
* Print the data set to make sure the file was read correctly;
PROC PRINT DATA = contest;
   TITLE 'Pumpkin Carving Contest';
RUN;
```
- The variable Name has an informat of $16., meaning that it is a character variable 16 columns wide.
- Variable Age has an informat of three, is numeric, three columns wide, and has no decimal places. The +1 skips over one column. 
- Variable Type is character, and it is one column wide. 
- Variable Date has an informat MMDDYY10. and reads dates in the form 10-31-2003 or 10/31/2003, each 10 columns wide. 
- The remaining variables, Score1 through Score5, all require the same informat, 4.1. By putting the variables and the informat in separate sets of parentheses, you only have to list the informat once. 

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553388523(1).png?raw=true)

## 1.2 Reading Multiple Lines of Raw Data per Observation

**Raw Data**
![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553391665(1).jpg?raw=true)

```SAS
* Create a SAS data set named highlow;
* Read the data file using line pointers;
DATA highlow;
   INFILE 'c:\MyRawData\Temperature.dat';
   INPUT City $ State $ 
         / NormalHigh NormalLow
         #3 RecordHigh RecordLow;
RUN;
```
The input statement reads the values of City and State from the first line. Then the slash tells SAS to move to column 1 of the next line before reading NormalHigh and NormalLow. #3 tells SAS to move to column1 of the third line before reading RecordHigh and RecordLow. **We also can replace the slash with #2 or replace #3 with a slash**.

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553391689(1).jpg?raw=true)

## 1.3 Controlling Input with Options in the INFILE Statement

- **FIRSTOBS**: The FIRSTOBS= option tells SAS at what line to begin reading data

- **OBS**: The OBS= option can be used anytime you want to read only a part of your data file. It tells SAS to stop reading when it gets to that line in the raw data file.

- **MISSOVER, TURNOVER** : Both will assign missing values to variables if the data line ends before the variable’s field starts. But when the data line ends in the middle of a variable field, TRUNCOVER will take as much as is there, whereas MISSOVER will assign the variable a missing value.

- **DLM, DSD**: useful for Delimiter files

## 1.4 Reading Files with the IMPORT Procedure

### CSV
```SAS
PROC IMPORT DATAFILE ='c:\MyRawData\Bands2.csv' OUT = music REPLACE;
RUN;
PROC PRINT DATA = music;    
   TITLE 'Customers at Each Gig'; RUN;
```
### XLS
```SAS
PROC IMPORT DATAFILE = 'c:\MyExcelFiles\OnionRing.xls' DBMS=XLS OUT = sales;
RUN;
PROC PRINT DATA = sales;
   TITLE 'SAS Data Set Read From Excel File'; RUN;
```

## 1.5 Temporary versus Permanent SAS Data Sets

**Creating a permanent SAS data set**

```SAS
LIBNAME plants 'c:\MySASLib';
DATA plants.magnolia;
   INFILE 'c:\MyRawData\Mag.dat';
   INPUT ScientificName $ 1-14 CommonName $ 16-32 MaximumHeight
      AgeBloom Type $ Color $;
RUN;
```

**Reading a permanent SAS data set**

```SAS
LIBNAME example 'c:\MySASLib';
PROC PRINT DATA = example.magnolia;
   TITLE 'Magnolias';
RUN;
```

**Temporary SAS data sets** (WORK.)

The following program creates and then prints a temporary SAS data set named DISTANCE:

```SAS
DATA distance;
Miles = 26.22;
Kilometers = 1.61 * Miles;
PROC PRINT DATA = distance;
RUN;
```

## 1.6 Listing the Contents of a SAS Data Set

```SAS
DATA funnies;
INPUT Id Name $ Height Weight DoB MMDDYY8.;
LABEL Id = ’Identification no.’
Height = ’Height in inches’
Weight = ’Weight in pounds’
DoB = ’Date of birth’;
INFORMAT DoB MMDDYY8.;
FORMAT DoB WORDDATE18.;
DATALINES;
53 Susie 42 41 07-11-81
54 Charlie 46 55 10-26-54
55 Calvin 40 35 01-10-81
56 Lucy 46 52 01-13-55
;


* Use PROC CONTENTS to describe data set funnies;
PROC CONTENTS DATA = funnies;
RUN;
```

## 2. Working with SAS Dates

```SAS
DATA librarycards;
   INFILE 'c:\MyRawData\Library.dat' TRUNCOVER;
   INPUT Name $11. + 1 BirthDate MMDDYY10. +1 IssueDate ANYDTDTE10.
      DueDate DATE11.;
   DaysOverDue = TODAY() - DueDate;
   Age = INT(YRDIF(BirthDate, TODAY(), 'AGE'));
   IF IssueDate > '01JAN2008'D THEN NewCard = 'yes';
RUN;
PROC PRINT DATA = librarycards;
   FORMAT Issuedate MMDDYY8. DueDate WEEKDATE17.;
   TITLE 'SAS Dates without and with Formats';
RUN;
```
![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553475068(1).png?raw=true)

Notice that the variable BirthDate is printed without a date format, while IssueDate and DueDate use formats.

### 2.1 YRDIF Function

- Returns the difference in years between two dates according to specified day count conventions; 
- Returns a person’s age.

### Example 1: Calculating a Difference in Years Based on Basis

In the following example, YRDIF returns the difference in years between two dates based on each of the options for basis.

```SAS
data _null_;
   sdate='16oct1998'd;
   edate='16feb2010'd;
   y30360=yrdif(sdate, edate, '30/360');
   yactact=yrdif(sdate, edate, 'ACT/ACT');
   yact360=yrdif(sdate, edate, 'ACT/360');
   yact365=yrdif(sdate, edate, 'ACT/365');
   put y30360= / yactact= / yact360= / yact365= ;
run;
```

SAS writes the following output to the log:
```SAS
y30360=11.333333333
yactact=11.336986301
yact360=11.502777778
yact365=11.345205479
```

### Example 2: Calculating a Person’s Age
You can calculate a person’s age by using three arguments in the YRDIF function. The third argument, basis, must have a value of AGE:

```SAS
data _null_;
   sdate='16oct1998'd;
   edate='16feb2010'd;
   age=yrdif(sdate, edate, 'AGE');
   put age= 'years';
run;
```
SAS writes the following output to the log:
```SAS
age=11.336986301 years
```

## 3. Using the RETAIN and Sum Statements

```SAS
* Using RETAIN and sum statements to find most runs and total runs;
DATA gamestats;
   INFILE 'c:\MyRawData\Games.dat';
   INPUT Month 1 Day 3-4 Team $ 6-25 Hits 27-28 Runs 30-31;
   RETAIN MaxRuns;
   MaxRuns = MAX(MaxRuns, Runs);
   RunsToDate + Runs;
RUN;
PROC PRINT DATA = gamestats;
   TITLE "Season's Record to Date";
RUN;
```
![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553487468(1).png?raw=true)

MaxRuns shows the maximum number of runs in a game to date, the RETAIN statement and MAX function to determine the maximum number of runs in a game to date;
RunsToDate shows the cumulative number of runs for the season.

## 4. Arrays

```SAS
DATA songs;    
INFILE 'c:\MyRawData\WBRK.dat';    
INPUT City $ 1-15 Age domk wj hwow simbh kt aomm libm tr filp ttr;
   ARRAY new (10) Song1 - Song10;
   ARRAY old (10) domk -- ttr;
   DO i = 1 TO 10;
      IF old(i) = 9 THEN new(i) = .;
         ELSE new(i) = old(i);
   END;
   AvgScore = MEAN(OF Song1 - Song10);
PROC PRINT DATA = songs;
   TITLE 'WBRK Song Survey';
RUN;
```
Note that both ARRAY statements use abbreviated variable lists array NEW uses a numbered range list and array OLD uses a name range list. Inside the iterative DO loop, the Song variables (array NEW) are set equal to missing if the original variable (array OLD) had a value of 9. Otherwise, they are set equal to the original values. After the DO loop, a new variable, AvgScore, is created using an abbreviated variable list in the function MEAN. The output includes variables from both the OLD array (domk, wj, ... ttr) and NEW array (Song1 - Song10).

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553491732(1).jpg?raw=true)


## 5. Printing Data with PROC PRINT

**PROC PRINT DATA = data-set NOOBS LABEL;**


- BY variable-list

   The BY statement starts a new section in the output for each new value of the BY variables and prints the values of the BY variables at the top of each section. The data must be presorted by the BY variables.

- ID variable-list

   When you use the ID statement, the observation numbers are not printed. Instead, the variables in the ID variable list appear on the left-hand side of the page.

- SUM variable-list

   The SUM statement prints sums for the variables in the list.

- **VAR variable-list**

   The VAR statement specifies which variables to print and the order. Without a VAR statement, all variables in the SAS data set are printed in the order that they occur in the data set.

**Raw Date**
![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553497047(1).jpg?raw=true)

``` SAS
DATA sales;    
INFILE 'c:\MyRawData\Candy.dat';    
INPUT Name $ 1-11 Class @15 DateReturned MMDDYY10. CandyType $ Quantity;
   Profit = Quantity * 1.25;
PROC SORT DATA = sales; BY Class;
PROC PRINT DATA = sales;
   BY Class;
   SUM Profit;
   VAR Name DateReturned CandyType Profit;
   TITLE 'Candy Sales for Field Trip by Class';
RUN;
```

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553496950(1).jpg?raw=true)

## 6. Summarizing Data Using PROC MEANS

**Raw Data**

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553560057(1).jpg?raw=true)

```SAS
DATA sales;    
   INFILE 'F:\SAS\MyRawData4\Flowers.dat';    
   INPUT CustomerID $ @9 SaleDate MMDDYY10. Petunia SnapDragon
         Marigold;    
   Month = MONTH(SaleDate); 
PROC SORT DATA = sales;    
   BY Month; * Calculate means by Month for flower sales;
PROC MEANS DATA = sales;    
   BY Month;    
   VAR Petunia SnapDragon Marigold;
   TITLE 'Summary of Flower Sales by Month';
RUN;
```

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553561291(1).png?raw=true)

**Writing Summary Statistics**

```SAS
DATA sales;    
   INFILE 'c:\MyRawData\Flowers.dat';
   INPUT CustomerID $ @9 SaleDate MMDDYY10. Petunia SnapDragon Marigold;
PROC SORT DATA = sales;
   BY CustomerID;
* Calculate means by CustomerID, output sum and mean to new data set;
PROC MEANS NOPRINT DATA = sales;
   BY CustomerID;
   VAR Petunia SnapDragon Marigold;
   OUTPUT OUT = totals  MEAN(Petunia SnapDragon Marigold) =
          MeanPetunia MeanSnapDragon MeanMarigold
      SUM(Petunia SnapDragon Marigold) = Petunia SnapDragon Marigold;
PROC PRINT DATA = totals;
   TITLE 'Sum of Flower Data over Customer ID';
   FORMAT MeanPetunia MeanSnapDragon MeanMarigold 3.;
RUN;
```

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553563259(1).jpg?raw=true)


## 7. Counting Data with PROC FREQ

### 7.1 two way

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553564381(1).png?raw=true)

```SAS
DATA orders;
   INFILE 'F:\SAS\MyRawData4\Coffee.dat';
   INPUT Coffee $ Window $ @@;run;
* Print tables for Window and Window by Coffee;
PROC FREQ DATA = orders;
   TABLES Window  Window * Coffee;
   RUN;

* Or table of window by coffee;
PROC TABULATE DATA=orders;
  CLASS Window Coffee;
  TABLE Window='',Coffee*N='';
RUN;
```

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553564436(1).jpg?raw=true)

### 7.2 three way (PROC TABULATE)

```SAS
DATA boats;
   INFILE 'c:\MyRawData\Boats.dat';
   INPUT Name $ 1-12 Port $ 14-20 Locomotion $ 22-26 Type $ 28-30 
      Price 32-36;
RUN;
* Tabulations with three dimensions;
PROC TABULATE DATA = boats;
   CLASS Port Locomotion Type;
   TABLE Port, Locomotion, Type;
   TITLE 'Number of Boats by Port, Locomotion, and Type';
RUN;
```
PROC TABULATE creates a three-dimensional report with the values of Port for the pages, Locomotion for the rows, and Type for the columns.

## 8. PROC TABULATE

### 8.1 Adding Statistics to PROC TABULATE Output

```SAS
PROC TABULATE;
   VAR analysis-variable-list;
   CLASS classification-variable-list;
   TABLE page-dimension, row-dimension, column-dimension;
```
Concatenating, crossing, and grouping Within a dimension.

keywords: ALL (adds a row, column, or page showing the total), MAX, MIN, MEAN, MEDIAN, P90.......

Variables and keywords can be concatenated, crossed, or grouped: 
- To concatenate variables or keywords simply list them separated by a space;
- To cross variables or keywords separate then with an asterisk (*);
- To group them enclose the variables or keywords in parentheses. 

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553583557(1).png?raw=true)
For example:
Concatenating: TABLE Locomotion Type ALL;
Crossing: TABLE MEAN * Price;
Crossing, grouping, and concatenating: TABLE PCTN *(Locomotion Type).

```SAS
* Tabulations with two dimensions and statistics;
PROC TABULATE DATA = boats;
   CLASS Locomotion Type;
   VAR Price;
   TABLE Locomotion ALL, MEAN*Price*(Type ALL);
   TITLE 'Mean Price by Locomotion and Type';
RUN;
```

```SAS
* Tabulations with three dimensions and statistics;
PROC TABULATE DATA = boats;
   CLASS Port Locomotion Type;
   VAR Price;
   TABLE Port ALL, Locomotion ALL, MEAN*Price*(Type ALL);
   TITLE 'Mean Price by Locomotion and Type';
RUN;
```
![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553589984(1).jpg?raw=true)


### 8.2 Changing Headers in PROC TABULATE Output

```SAS
DATA boats;
   INFILE 'c:\MyRawData\Boats.dat';
   INPUT Name $ 1-12 Port $ 14-20 Locomotion $ 22-26 Type $ 28-30 
      Price 32-36;
RUN;
* Changing headers;
PROC FORMAT;
   VALUE $typ  'cat' = 'catamaran'
               'sch' = 'schooner'
               'yac' = 'yacht';
RUN;
PROC TABULATE DATA = boats FORMAT=DOLLAR9.2;
   CLASS Locomotion Type;
   VAR Price;
   FORMAT Type $typ.;
   TABLE Locomotion='' ALL, 
      MEAN=''*Price='Mean Price by Type of Boat'*(Type='' ALL)
      /BOX='Full Day Excursions' MISSTEXT='none';
   TITLE;
RUN;
```

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553589960(1).jpg?raw=true)

### 8.3 Specifying Multiple Formats for Data Cells in PROC TABULATE Output

```sas
* Using the FORMAT= option in the TABLE statement;
PROC TABULATE DATA = boats;
   CLASS Locomotion Type;
   VAR Price Length;
   TABLE Locomotion ALL,
      MEAN * (Price*FORMAT=DOLLAR6.2 Length*FORMAT=6.0) *(Type ALL);
   TITLE 'Price and Length by Type of Boat';
RUN;
```

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553590507(1).jpg?raw=true)

## 9. PROC REPORT

### 9.1 Creating Summary Reports with PROC REPORT
```SAS
DEFINE variable / options ’column-header’
```
Options:

- ACROSS creates a column for each unique value of the variable.
- ANALYSIS calculates statistics for the variable. This is the default usage for numeric variables, and the default statistic is sum.
- DISPLAY creates one row for each observation in the data set. This is the default usage for character variables.
- GROUP creates a row for each unique value of the variable.
- ORDER creates one row for each observation with rows arranged according to the values of the order variable.

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553592271(1).png?raw=true)
While the GROUP usage type produces **summary rows**, the ACROSS usage type produces **summary columns**.

```SAS
DATA natparks;
   INFILE 'F:\SAS\MyRawData4\Parks.dat';
   INPUT Name $ 1-21 Type $ Region $ Museums Camping;
RUN;

* Region and Type as GROUP variables;
PROC REPORT DATA = natparks NOWINDOWS HEADLINE;
   COLUMN Region Type Museums Camping;
   DEFINE Region / GROUP;
   DEFINE Type / GROUP;
   TITLE 'Summary Report with Two Group Variables';
RUN;
* Region as GROUP and Type as ACROSS with sums;
PROC REPORT DATA = natparks NOWINDOWS HEADLINE;
   COLUMN Region Type,(Museums Camping);
   DEFINE Region / GROUP;
   DEFINE Type / ACROSS;
   TITLE 'Summary Report with a Group and an Across Variable';
RUN;
```

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553592568(1).jpg?raw=true)

### 9.2 Adding Statistics to PROC REPORT Output

To request a statistic for a particular variable, insert a comma between the statistic and variable in the COLUMN statement. One statistic, N, does not require a comma because it does not apply to a particular variable. If you insert N in a COLUMN statement, then SAS will print the number of observations that contributed to that row of the report. This statement tells SAS to print two columns of data: the median of a variable named Age, and the number of observations in that row:
**COLUMN Age,MEDIAN N;**

To request multiple statistics or statistics for multiple variables, put parentheses around the statistics or variables. This statement uses parentheses to request two statistics for the variable Age, and then requests one statistic for two variables, Height and Weight.
**COLUMN Age,(MIN MAX) (Height Weight),MEAN;**


```SAS
DATA natparks;
   INFILE 'c:\MyRawData\Parks.dat';
   INPUT Name $ 1-21 Type $ Region $ Museums Camping;
RUN;
*Statistics in COLUMN statement with two group variables;
PROC REPORT DATA = natparks NOWINDOWS HEADLINE;
   COLUMN Region Type N (Museums Camping),MEAN;
   DEFINE Region / GROUP;
   DEFINE Type / GROUP;
   TITLE 'Statistics with Two Group Variables';
RUN;
*Statistics in COLUMN statement with group and across variables;
PROC REPORT DATA = natparks NOWINDOWS HEADLINE;
   COLUMN Region N Type,(Museums Camping),MEAN;
   DEFINE Region / GROUP;
   DEFINE Type / ACROSS;
   TITLE 'Statistics with a Group and Across Variable';
RUN;
```
![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553593758(1).jpg?raw=true)


## 10. COMPUTE new variables that are numeric and character

```SAS
DATA natparks;
   INFILE 'c:\MyRawData\Parks.dat';
   INPUT Name $ 1-21 Type $ Region $ Museums Camping;
RUN;

* COMPUTE new variables that are numeric and character;
PROC REPORT DATA = natparks NOWINDOWS HEADLINE;
   COLUMN Name Region Museums Camping Facilities Note;
   DEFINE Museums / ANALYSIS SUM NOPRINT;
   DEFINE Camping / ANALYSIS SUM NOPRINT;
   DEFINE Facilities / COMPUTED 'Camping/and/Museums';
   DEFINE Note / COMPUTED;
   COMPUTE Facilities;
      Facilities = Museums.SUM + Camping.SUM;
   ENDCOMP;
   COMPUTE Note / CHAR LENGTH = 10;
      IF Camping.SUM = 0 THEN Note = 'No Camping';
   ENDCOMP;
   TITLE 'Report with Two Computed Variables'; 
RUN;
```
![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553645546(1).jpg?raw=true)

## 11. Define formats to group the data

```SAS
DATA books;
   INFILE 'c:\MyRawData\LibraryBooks.dat';
   INPUT Age BookType $ @@;
RUN;
*Define formats to group the data;
PROC FORMAT;
   VALUE agegpa
         0-18    = '0 to 18'
         19-25   = '19 to 25'
         26-49   = '26 to 49'
         50-HIGH = '  50+ ';
   VALUE agegpb
         0-25    = '0 to 25'
         26-HIGH = '  26+ ';
   VALUE $typ
        'bio','non','ref' = 'Non-Fiction'
        'fic','mys','sci' = 'Fiction';
RUN;
*Create two way table with Age grouped into four categories;
PROC FREQ DATA = books;
   TITLE 'Patron Age by Book Type: Four Age Groups';
   TABLES BookType * Age / NOPERCENT NOROW NOCOL;
   FORMAT Age agegpa. BookType $typ.;
RUN;
*Create two way table with Age grouped into two categories;
PROC FREQ DATA = books;
   TITLE 'Patron Age by Book Type: Two Age Groups';
   TABLES BookType * Age / NOPERCENT NOROW NOCOL;
   FORMAT Age agegpb. BookType $typ.;
RUN;
```

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553645576(1).jpg?raw=true)


## 12. Adding Traffic-Lighting to Your Output

```SAS
PROC FORMAT;
  VALUE rec 0 -< 378.72 ='red'
            378.72 -< 382.20 = 'orange'
            382.20 - HIGH = 'white';
RUN;
PROC PRINT DATA=results;
  ID Place;
  VAR Name Country;
  VAR Time/STYLE={BACKGROUND=rec.};
  TITLE 'Men''s 5000m Speed Skating';
  TITLE2 '2002 Olympic Results';
RUN;
```

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553830214(1).jpg?raw=true)

## 13. Merge

### 13.1 One-to-One Match Merge / One-to-Many Match Merge

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/cc8b3b72c9bce7b6077cae7272f4144.png?raw=true)

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553838861(1).jpg?raw=true)


```SAS
* Merge data sets by Samecol;
DATA final_dataset;    
   MERGE dataset1 dataset2;    
   BY Same_col;
```

### 13.2 Combining a Grand Total with the Original Data (no common variable to merge by)

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553848545(1).png?raw=true)


```SAS
DATA shoes;
   INFILE 'c:\MyRawData\Shoesales.dat';
   INPUT Style $ 1-15 ExerciseType $ Sales;
RUN;
* Output grand total of sales to a data set and print;
PROC MEANS NOPRINT DATA = shoes;
   VAR Sales;
   OUTPUT OUT = summarydata SUM(Sales) = GrandTotal;
PROC PRINT DATA = summarydata;
   TITLE 'Summary Data Set';
RUN;
* Combine the grand total with the original data (shoes);
DATA shoesummary;
   IF _N_ = 1 THEN SET summarydata;
   SET shoes;
   Percent = Sales / GrandTotal * 100;
PROC PRINT DATA = shoesummary;
   VAR Style ExerciseType Sales GrandTotal Percent;
   TITLE 'Overall Sales Share';
RUN;
```

## 14. Making Several Observations from One Using the OUTPUT Statement

![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553903620(1).jpg?raw=true)


**Raw Data**
![image](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553903699(1).jpg?raw=true)

```SAS
* Create three observations for each data line read
* using three OUTPUT statements;
DATA theaters;
   INFILE 'c:\MyRawData\Movies.dat';
   INPUT Month $ Location $ Tickets @;
   OUTPUT;
   INPUT Location $ Tickets @;
   OUTPUT;
   INPUT Location $ Tickets;
   OUTPUT;
PROC PRINT DATA = theaters;
   TITLE 'Ticket Sales';
RUN;
```

- The first INPUT statement reads values for Month, Location, and Tickets, and then holds the data line using the trailing at sign (@). The OUTPUT statement that follows writes an observation. 

- The next INPUT statement reads the second set of data for Location and Tickets and again holds the data line. Another OUTPUT statement writes another observation. Month still has the same value because it isn’t in the second INPUT statement. 

-  The last INPUT statement reads the last values for Location and Tickets, this time releasing the data line for the next iteration through the DATA step. The final OUTPUT statement writes the third observation for that iteration of the DATA step.

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553904091(1).jpg?raw=true)


## 15. Changing Observations to Variables Using PROC TRANSPOSE

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553904223(1).jpg?raw=true)

```SAS
DATA baseball;
   INFILE 'c:\MyRawData\Transpos.dat';
   INPUT Team $ Player Type $ Entry;
PROC SORT DATA = baseball;
   BY Team Player;
PROC PRINT DATA = baseball;
TITLE 'Baseball Data After Sorting and Before Transposing';

* Transpose data so salary and batavg are variables;
PROC TRANSPOSE DATA = baseball OUT = flipped;
   BY Team Player;
   ID Type;
   VAR Entry;
PROC PRINT DATA = flipped;
TITLE 'Baseball Data After Transposing';
RUN;
```

![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553906305(1).jpg?raw=true)


## 16. Macros


### 16.1 

```SAS
* Macro with parameters;
%MACRO select(customer=,sortvar=);
   PROC SORT DATA = flowersales OUT = salesout;
      BY &sortvar;
      WHERE CustomerID = "&customer";
   RUN;
   PROC PRINT DATA = salesout;
      FORMAT SaleDate WORDDATE18.;
      TITLE1 "Orders for Customer Number &customer";
      TITLE2 "Sorted by &sortvar";
   RUN;
%MEND select;

* Read all the flower sales data;
DATA flowersales;
   INFILE 'c:\MyRawData\TropicalSales.dat';
   INPUT CustomerID $4. @6 SaleDate MMDDYY10. @17 Variety $9. Quantity;
RUN;

*Invoke the macro;
%select(customer = 356W, sortvar = Quantity)
%select(customer = 240W, sortvar = Variety)
```

### 16.2 Writing Macros with Conditional Logic

```SAS
%MACRO dailyreports;
   %IF &SYSDAY = Monday %THEN %DO;
      PROC PRINT DATA = flowersales;
         FORMAT SaleDate WORDDATE18.;
         TITLE 'Monday Report: Current Flower Sales';
      RUN;
   %END;
   %ELSE %IF &SYSDAY = Tuesday %THEN %DO;
      PROC MEANS DATA = flowersales MEAN MIN MAX;
         CLASS Variety;
         VAR Quantity;
         TITLE 'Tuesday Report: Summary of Flower Sales';
      RUN;
   %END;
%MEND dailyreports;

DATA flowersales;
   INFILE 'c:\MyRawData\TropicalSales.dat';
   INPUT CustomerID $4. @6 SaleDate MMDDYY10. @17 Variety $9. Quantity;
RUN;
%dailyreports

```

## 17. PROC CORR (Pearson Correlation)

```SAS
PROC CORR DATA = class;
VAR Television Exercise; 
WITH Score;
TITLE ’Correlations for Test Scores’;
TITLE2 ’With Hours of Television and Exercise’;
RUN;
```
![IMAGE](https://github.com/manpanmanpan/manpanmanpan.github.io/blob/master/img/1553994327(1).png?raw=true)

This report starts with descriptive statistics for each variable and then lists the correlation matrix which contains: (1) correlation coefficients (in this case, Pearson), and (2) the probability of getting a larger absolute value for each correlation.

In this example, both hours of television and hours of exercise are correlated with test score, but exercise is positively correlated while television is negatively correlated. This means students who watched more television tended to have lower scores, while the students who spent more time exercising tended to have higher scores.

## 18. PROC REG

```SAS
DATA hits;
INFILE 'c:\MyRawData\Baseball.dat';
INPUT Height Distance @@;
* Perform regression analysis, plot observed values with regression line;
PROC REG DATA = hits;
MODEL Distance = Height;
PLOT Distance * Height;
TITLE 'Results of Regression Analysis';
RUN;
```

## PROC ANOVA

```SAS
DATA basket;
INFILE 'c:\MyRawData\Basketball.dat';
INPUT Team $ Height @@;
* Use ANOVA to run one-way analysis of variance;
PROC ANOVA DATA = basket;
CLASS Team;
MODEL Height = Team;
MEANS Team / SCHEFFE;
TITLE ”Girls' Heights on Basketball Teams”;
RUN;
```
